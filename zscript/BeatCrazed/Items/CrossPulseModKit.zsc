// ------------------------------------------------------------
// Cross Pulse Modification Kit
// For people who want the X-Pulse TB but don't want to start with it in their inventory
// ------------------------------------------------------------

class CrossPulseModificationKit : HDPickup {
    action void A_Activate() {		
        //checks current weapon in hands instead of scrounging inventory
        let sacrificeTB = ThunderBuster(player.readyweapon);

        if (sacrificeTB) {
            bool garbage;
            Actor newTB;

            // spits it out instead of dropping at feet
            [garbage, newTB] = self.A_SpawnItemEx(
                'CrossPulseThunderBuster',
                cos(pitch) * 12,0, height - 8 - sin(pitch) * 12, cos(pitch) * (random(10, 20) * 0.1),
                0.2 * randompick(-1, 1), -sin(pitch), 0,
                SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
            );

            let buster = CrossPulseThunderBuster(newTB);
            buster.weaponstatus[TBS_FLAGS] = sacrificeTB.weaponstatus[TBS_FLAGS];
            buster.weaponstatus[TBS_BATTERY] = sacrificeTB.weaponstatus[TBS_BATTERY];
            
            sacrificeTB.destroy();
            self.takeinventory('CrossPulseModificationKit',1);
        } else {
            A_Log(StringTable.Localize("$XTK_NO_TB"));
        }
    }

    default {
        +hdpickup.norandombackpackspawn;
        +Inventory.InvBar;

        Tag "$TAG_XTK";
        Inventory.PickupMessage "$PICKUP_XTK";
        HDPickup.RefID HDLD_XTK;
        HDPickup.Bulk 40;
        Scale 0.5;
    }
    
    states {
        Spawn:
            FKIT A 1;
            loop;
        Use:
            TNT1 A 0 A_Activate();
    }
}
